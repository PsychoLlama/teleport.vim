Before (hook into script functions):
  source autoload/zcd.vim

  let $ZSH = ''
  if exists('g:zcd#path')
    unlet g:zcd#path
  endif

  " Locate the zcd.vim source file and associated script number.
  func! g:FindSourceFile()
    let l:scriptnames = ''
    redir => l:scriptnames
    silent scriptnames
    redir END

    " Get the zcd.vim script ID.
    for l:line in split(l:scriptnames, "\n")
      let l:is_autoload = l:line =~# '\Vautoload/zcd.vim'
      let l:not_plugin = l:line !~# 'plugged'
      let l:not_tests = l:line !~# 'tests'
      if l:is_autoload && l:not_plugin && l:not_tests
        let l:SID = str2nr(matchstr(l:line, '\v^\s*\d+'))
        let l:zcd_file = matchstr(l:line, '\v(\~|/).*')
        return { 'SID': l:SID, 'file': expand(l:zcd_file) }
      endif
    endfor

    return v:null
  endfunc

  " Find the oh-my-zsh test fixture.
  func! g:GetFakeZshPath()
    let l:src = g:FindSourceFile()
    let l:repo = fnamemodify(l:src.file, ':h:h')

    return l:repo . '/tests/fake-oh-my-zsh'
  endfunc

  " Invoke script-scoped functions in autoload/zcd.vim.
  func! g:Private(func_name, ...) abort
    let l:source = g:FindSourceFile()

    " Figure out the prefixed function name.
    let l:local_func = '<SNR>' . l:source.SID . '_' . a:func_name
    let l:Fn = function(l:local_func, a:000)

    " Ignore calls to :echo.
    silent return l:Fn()
  endfunc
